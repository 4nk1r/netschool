plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
    id 'kotlin-parcelize'
    id 'org.jlleitschuh.gradle.ktlint'
}

android {
    namespace 'io.fournkoner.netschool'
    compileSdk rootProject.ext.compileSdk

    defaultConfig {
        applicationId "io.fournkoner.netschool"
        minSdk rootProject.ext.minSdk
        targetSdk rootProject.ext.targetSdk
        versionCode 4
        versionName "0.1.3"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            resValue "string", "app_name", "DaSchool"
        }
        release {
            resValue "string", "app_name", "NetSchool"

            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
        coreLibraryDesugaringEnabled true
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.4.7'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    hilt {
        enableAggregatingTask = true
    }
}

dependencies {
    // Project
    implementation(project(":domain"))
    implementation(project(":data"))
    // AndroidX Stuff
    implementation libs.bundles.androidx
    // Jetpack Compose
    implementation platform(libs.composeBom)
    implementation libs.bundles.compose
    // Voyager
    implementation libs.bundles.voyager
    // Accompanist
    implementation libs.bundles.accompanist
    // Date picker
    implementation libs.bundles.sheetsComposeDialogs
    // Splitties
    implementation libs.splitties
    // Hilt
    implementation libs.hilt
    implementation libs.hiltCompose
    kapt libs.hiltCompiler
    // Paging
    implementation libs.pagingCompose
    // Java Desugar
    coreLibraryDesugaring libs.javaDesugar
}

kapt {
    correctErrorTypes true
}